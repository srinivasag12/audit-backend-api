CREATE OR REPLACE PROCEDURE AUDIT_STAG_TEST.CERTIFICATE_EXPIRE_SCHED_PRC
IS
   v_certificate_no         VARCHAR2 (50);
   v_audit_seq_no           NUMBER;
   v_cert_expiry_date       DATE;
   v_audit_type_id          NUMBER;
   v_audit_subtype_id       NUMBER;
   v_company_id             NUMBER;
   v_tot_days               NUMBER;
   v_sub                    VARCHAR2 (4000);
   v_bdy                    VARCHAR2 (4000);
   audit_desc_msg           VARCHAR (200);
   v_certificate_issue_id   NUMBER;
   v_mgr                    VARCHAR2 (500);
   v_admin                  VARCHAR2 (500);
   v_from                   VARCHAR2 (4000) := audit_details_pkg.v_from;
   v_to                     VARCHAR2 (4000);
   v_cc                     VARCHAR2 (4000);
   vErrMsg                  VARCHAR2 (4000);
   v_vessel_name            VARCHAR2 (200);
   v_vessel_imo             NUMBER;
   v_official_no            NUMBER;
BEGIN
   FOR i
      IN (SELECT DISTINCT (AUDIT_SEQ_NO), COMPANY_ID
            FROM CERTIFICATE_DETAIL
           WHERE ACTIVE_STATUS = 1
                 AND TO_DATE (
                        (CASE
                            WHEN EXTENDED_EXPIRY_DATE IS NOT NULL
                            THEN
                               EXTENDED_EXPIRY_DATE
                            ELSE
                               CERT_EXPIRY_DATE
                         END),
                        'DD/MM/RRRR')
                     - TRUNC (SYSDATE) BETWEEN 0
                                           AND 90)
   LOOP
      v_audit_seq_no := i.AUDIT_SEQ_NO;
      v_company_id := i.COMPANY_ID;

      SELECT CERTIFICATE_NO,
             AUDIT_TYPE_ID,
             AUDIT_SUB_TYPE_ID,
             CERTIFICATE_ISSUE_ID,
             CERTIFICATE_NO,
             VESSEL_IMO_NO,
             VESSEL_NAME,
             OFFICIAL_NO,
             (TO_DATE (
                 (CASE
                     WHEN EXTENDED_EXPIRY_DATE IS NOT NULL
                     THEN
                        EXTENDED_EXPIRY_DATE
                     ELSE
                        CERT_EXPIRY_DATE
                  END),
                 'DD/MM/RRRR')
              - TRUNC (SYSDATE))
                AS total_days
        INTO v_certificate_no,
             v_audit_type_id,
             v_audit_subtype_id,
             v_certificate_issue_id,
             v_certificate_no,
             v_vessel_imo,
             v_vessel_name,
             v_official_no,
             v_tot_days
        FROM CERTIFICATE_DETAIL
       WHERE audit_seq_no = v_audit_seq_no AND company_id = v_company_id
             AND seq_no =
                    (SELECT MAX (seq_no)
                       FROM CERTIFICATE_DETAIL
                      WHERE audit_seq_no = v_audit_seq_no
                            AND company_id = v_company_id);


      IF v_audit_type_id IN (1004, 1005)
      THEN
         audit_desc_msg := 'Review';
      ELSIF v_audit_type_id IN (1003)
      THEN
         audit_desc_msg := 'Inspection';
      ELSIF v_audit_type_id IN (1001, 1002)
      THEN
         audit_desc_msg := 'Audit';
      END IF;

      SELECT subject, body
        INTO v_sub, v_bdy
        FROM audit_email_dtls
       WHERE email_seq = 58;

      SELECT muliple_replace_fun (
                v_sub,
                NEW t_text ('No.__', 'in__'),
                NEW t_text ('No: ' || v_certificate_no, 'in ' || v_tot_days)),
             muliple_replace_fun (
                v_bdy,
                NEW t_text ('Audit Type__',
                            'Audit Subtype__',
                            'Certificate Type__',
                            'Certificate No.___',
                            'Vessel:__',
                            'IMO No.:___',
                            'Official No.:___',
                            'in ____'),
                NEW t_text (
                       audit_desc_msg || ' Type: ' || audit_type_desc,
                       audit_desc_msg || ' Subtype: ' || audit_subtype_desc,
                       'Certificate ' || 'Type: ' || certificate_type_desc,
                       'Certificate No: ' || v_certificate_no,
                       'Vessel: ' || v_vessel_name,
                       'IMO No: ' || v_vessel_imo,
                       'Official No: ' || v_official_no,
                       'in ' || v_tot_days))
        INTO v_sub, v_bdy
        FROM (SELECT (SELECT AUDIT_TYPE_DESC
                        FROM MA_AUDIT_TYPE
                       WHERE AUDIT_TYPE_ID = v_audit_type_id
                             AND COMPANY_ID = v_company_id)
                        audit_type_desc,
                     (SELECT AUDIT_SUBTYPE_DESC
                        FROM MA_AUDIT_SUBTYPE
                       WHERE     AUDIT_TYPE_ID = v_audit_type_id
                             AND AUDIT_SUB_TYPE_ID = v_audit_subtype_id
                             AND COMPANY_ID = v_company_id)
                        audit_subtype_desc,
                     (SELECT CERTIFICATE_ISSUE_DESC
                        FROM MA_CERTIFICATE_ISSUED
                       WHERE CERTIFICATE_ISSUE_ID = v_certificate_issue_id
                             AND AUDIT_TYPE_ID = v_audit_type_id
                             AND COMPANY_ID = v_company_id)
                        certificate_type_desc
                FROM DUAL) test;

      SELECT listAgg (user_id, ',') WITHIN GROUP (ORDER BY role_id ASC)
        INTO v_cc
        FROM MA_USER_ROLES USRL
       WHERE USRL.ROLE_ID = 1003 AND company_id = v_company_id;

      SELECT listAgg (user_id, ',') WITHIN GROUP (ORDER BY role_id ASC)
        INTO v_to
        FROM MA_USER_ROLES USRL
       WHERE USRL.ROLE_ID = 1002 AND company_id = v_company_id;

      audit_send_mail_prc ('Certificate expiring date ' || v_audit_seq_no,
                           v_company_id,
                           v_from,
                           v_to,
                           v_cc,
                           v_sub,
                           v_bdy);
   END LOOP;
EXCEPTION
   WHEN OTHERS
   THEN
      SELECT subject
        INTO v_sub
        FROM audit_email_dtls
       WHERE email_seq = 58;

      DBMS_OUTPUT.put_line (
            '  OUTER BLOCK EXCEPTION '
         || SQLCODE
         || ' >> '
         || SQLERRM
         || '  >> '
         || DBMS_UTILITY.format_error_backtrace);
      vErrMsg := SQLERRM || ' on ' || DBMS_UTILITY.format_error_backtrace;
      audit_send_mail_prc (
         p_audit_no     => 'Certificate expiring Date  ' || v_audit_seq_no,
         p_company_id   => v_company_id,
         p_from_mail    => v_from,
         p_to_mail      => audit_details_pkg.V_BSOLTEAM,
         p_subject      => v_sub,
         p_body         => vErrMsg);
END;
/
